\ Basic control structures

\ Stack shuffling with conditionals

\ Basic comparisons

?: <=		> invert ;
?: >=		< invert ;
?: 0=		if 0 else 0 invert then ;
?: 0<		0 < ;
?: 0>		0 > ;
?: 0<>		0= 0= ;
?: NOT		0= ;
?: =		- 0= ;
?: <>		= invert ;
?: U<		2dup xor 0< if swap drop 0< else - 0< then ;
?: U>		swap u< ;
?: WITHIN	over - >r - r> u< ; 

\ Basic arithmetic

?: 1+		1 + ;
?: 1-		1 - ;

\ Basic memory access

?: CELL+	1 cells + ;
?: CHAR+	1 chars + ;

\ /SOURCE allows easy access to current character
?: /SOURCE	source drop ipos chars + ;

?: LITERAL	postpone LIT , ; immediate

?: CHAR		parse-name drop c@ ;
?: [CHAR]	char postpone literal ; immediate

\ Parse (advance input) to next )
?: (		[char] ) parse 2drop ; immediate

( Now we can use stack comments too )

?: ['] ( C: "<spaces>name" -- ) ( -- xt ) ' postpone literal ; immediate

?: CR ( -- ) 10 emit ;
?: TYPE ( c-addr u -- ) [: dup c@ emit char+ ;] times drop ;

?: .( ( "ccc<paren>" -- ) [CHAR] ) parse type ;

?: VARIABLE	create 0 , ;
?: CONSTANT	create , does> @ ;

\ --

?: SEARCH-WORDLIST	find-name-in dup if dup nt>xt swap immediate? if 1 else -1 then then ;

\ -- Conditional compilation ------------------------------
 
?: [DEFINED]	parse-name find-name 0<> ;
?: [UNDEFINED]	parse-name find-name 0= ;

\ Implementation of [IF] [ELSE] [THEN] by ruv, as seen in:
\ https://forth-standard.org/standard/tools/BracketELSE
 
wordlist dup constant BRACKET-FLOW-WL get-current swap set-current
: [IF]			1+ ;
: [ELSE]		dup 1 = if 1- then ;
: [THEN]		1- ;
set-current

: [ELSE] \ ( -- )
 	1 begin 
 		begin 
 			parse-name dup while
			bracket-flow-wl search-wordlist if 
				execute dup 0= if drop exit then
			then
 		repeat 
 		2drop refill 0= 
 	until 
 	drop
; immediate
 
: [THEN]	; immediate 
 
: [IF]		0= if postpone [else] then ; immediate

\ -- Test suite helper ------------------------------------

\ Set for the Java platform right now

: dotests s" ../../../forth2012-test-suite/src/runtests.fth" included ;
